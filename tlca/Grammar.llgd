uses "./Scanner.llld";

Program
    : Expression {";" Expression}
    ;

Expression
    : Relational {Relational}
    ;

Relational
    : Multiplicative ["==" Multiplicative]
    ;

Multiplicative
    : Additive {MultiplicativeOps Additive}
    ;

MultiplicativeOps
    : "*"
    | "/"
    ;

Additive
    : Factor {AdditiveOps Factor}
    ;

AdditiveOps
    : "+"
    | "-"
    ;

Factor
    : "(" [Expression {"," Expression}] ")"
    | LiteralInt
    | LiteralString
    | "True"
    | "False"
    | "\" Identifier {Identifier} "->" Expression
    | "let" ["rec"] Declaration {"and" Declaration} ["in" Expression]
    | "if" "(" Expression ")" Expression "else" Expression
    | Identifier
    | "match" Expression "with" ["|"] Case {"|" Case}
    ;

Declaration
    : Identifier {Identifier} "=" Expression
    ;

Case
    : Pattern "->" Expression
    ;

Pattern
    : "(" [Pattern {"," Pattern}] ")"
    | LiteralInt
    | LiteralString
    | "True"
    | "False"
    | Identifier
    ;
